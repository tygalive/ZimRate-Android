plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id("com.apollographql.apollo3").version("$apollo_version")
}

android {
    compileSdk 34
    buildToolsVersion = '34.0.0'
    defaultConfig {
        applicationId "com.tyganeutronics.myratecalculator"
        minSdk 21
        targetSdk 34
        versionCode 38
        versionName "2.0.4"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                compilerArgumentProviders(
                        new RoomSchemaArgProvider(new File(projectDir, "schemas"))
                )
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '.debug'
            multiDexKeepFile file('multidex-config.txt')
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    flavorDimensions 'store'
    productFlavors {
        playstore {
            dimension = 'store'
        }
        samsungstore {
            dimension = 'store'
        }
        otherstore {
            dimension = 'store'
        }
    }
    namespace 'com.tyganeutronics.myratecalculator'
}

dependencies {
    //desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation "androidx.core:core-splashscreen:1.0.1"

    // Appodeal Ads
    implementation('com.appodeal.ads:sdk:3.2.1.0') {
        exclude group: 'com.appodeal.ads.sdk.services', module: 'adjust'
        exclude group: 'com.appodeal.ads.sdk.services', module: 'appsflyer'
        exclude group: 'com.appodeal.ads.sdk.services', module: 'facebook_analytics'
        exclude group: 'com.appodeal.ads.sdk.services', module: 'sentry_analytics'
        exclude(group: "com.appodeal.ads.sdk.networks", module: "admob")
        exclude(group: "com.google.android.gms", module: "play-services-ads")
    }

    //Crashlytics
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:32.7.3')
    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-config'
    implementation("com.google.firebase:firebase-appcheck-playintegrity")

    //licenses dialog
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'

    //multi dex
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'org.jetbrains:annotations:24.1.0'

    //preferences
    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation("com.apollographql.apollo3:apollo-runtime:$apollo_version")
    implementation("com.apollographql.apollo3:apollo-api:$apollo_version")

    //https://github.com/maltaisn/calcdialoglib
    implementation "com.maltaisn:calcdialog:2.2.2"

    //https://developer.android.com/training/data-storage/room
    def room_version = '2.6.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'

    //play licensing
    def billing_version = '6.1.0'
    implementation("com.android.billingclient:billing:$billing_version")
    implementation("com.android.billingclient:billing-ktx:$billing_version")

    // Play In-App Review
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'

    // Play In-App Update
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
}

apollo {
    service("service") {
        packageName.set("com.tyganeutronics.myratecalculator.graphql")
    }
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        // Note: If you're using KSP, change the line below to return
        // ["room.schemaLocation=${schemaDir.path}".toString()].
        return ["-Aroom.schemaLocation=${schemaDir.path}".toString()]
    }
}